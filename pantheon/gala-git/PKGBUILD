# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala-git
pkgver=0.3.0.r199.2104b18
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=('x86_64')
url='https://github.com/elementary/gala'
license=('GPL3')
groups=('pantheon-unstable')
depends=('atk' 'bamf' 'cairo' 'gdk-pixbuf2' 'glib2' 'gnome-desktop' 'gtk3'
         'json-glib' 'libdrm' 'libgee' 'libgl' 'libx11' 'libxcomposite'
         'libxdamage' 'libxext' 'libxfixes' 'libxi' 'libxrandr' 'libxtst'
         'mesa' 'mutter' 'pango' 'plank' 'wayland'
         'libgranite.so')
makedepends=('git' 'gnome-common' 'granite-git' 'intltool' 'meson' 'vala')
provides=('gala' 'libgala.so')
conflicts=('gala')
source=('git+https://github.com/elementary/gala.git')
sha256sums=('SKIP')

pkgver() {
  cd gala

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

prepare() {
  cd gala

#  git cherry-pick -n bda9dc191f25f2473ce5b858cfc6398d0f1865ef
}

build() {
  arch-meson gala build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
